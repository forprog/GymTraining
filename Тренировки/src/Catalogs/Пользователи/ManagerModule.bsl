// @strict-types

#Область ПрограммныйИнтерфейс

// Возвращает текущего пользователя
// 
// Возвращаемое значение:
// СправочникСсылка.Пользователи 
Функция ПолучитьТекущегоПользователя() Экспорт

	Возврат ПараметрыСеанса.ТекущийПользователь;
	
КонецФункции

// Возвращает Пользователя для текущего пользователя ИБ. При необходимости создает новый элемент
// 
// Возвращаемое значение:
// СправочникСсылка.Пользователи 
Функция ИдентифицироватьТекущегоПользователя() Экспорт
	
	Пользователь = ПустаяСсылка();
	 
	Если ИмяПользователя() <> "" Тогда
		Пользователь = НайтиПользователяПоИмени(ИмяПользователя());
		Если Не ЗначениеЗаполнено(Пользователь) Тогда
			Пользователь = СоздатьПользователяПоИмени(ИмяПользователя());
		КонецЕсли;
	КонецЕсли;
	
	Возврат Пользователь;
	
КонецФункции

// Создает/обновляет пользователя ИБ для переданного Пользователя
//
// Параметры:
// 	Пользователь - СправочникОбъект.Пользователи - пользователь, для которого создается пользователь ИБ
// 	СтарыйКод - Строка - Старый код пользователя, чтобы идентифицировать его в ПользователяхИБ  
Процедура СоздатьОбновитьПользователяИБ(Пользователь, СтарыйКод) Экспорт
	
	Если СтарыйКод <> Неопределено Тогда
		текПользователь = ПользователиИнформационнойБазы.НайтиПоИмени(СтарыйКод);
	КонецЕсли;
	
	Если текПользователь = Неопределено Тогда
		текПользователь = ПользователиИнформационнойБазы.СоздатьПользователя();
	КонецЕсли;
	
	текПользователь.Имя = Пользователь.Код;
	текПользователь.ПолноеИмя = Пользователь.Наименование;
	текПользователь.Записать();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

//@skip-check module-accessibility-at-client
Функция СоздатьПользователяПоИмени(ИмяПользователя)
	
	НовыйПользовательОбъект = СоздатьЭлемент();
	НовыйПользовательОбъект.Код = ИмяПользователя;
	НовыйПользовательОбъект.Наименование = ИмяПользователя;
	НовыйПользовательОбъект.Записать();
	НовыйПользовательСсылка = НовыйПользовательОбъект.Ссылка;
	
	Возврат НовыйПользовательСсылка;
	
	
КонецФункции

//@skip-check module-accessibility-at-client
Функция НайтиПользователяПоИмени(ИмяПользователя)
	
	НайденныйПользователь = НайтиПоКоду(ИмяПользователя);
	
	Возврат НайденныйПользователь;
	
КонецФункции

#КонецОбласти
